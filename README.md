# Надстройка над крейтом **[jsonschema](https://crates.io/crates/jsonschema)** для 1С

Поддерживаются схемы:

- Draft 7
- Draft 6
- Draft 4

**Draft 2019-09** и **Draft 2020-12** в библиотеке **[jsonschema](https://crates.io/crates/jsonschema)** поддерживаются не полностью, поэтому в компоненту они не включены. Будут включены в компоненту после полной реализации в библиотеке

В случае если может произойти исключение, в описании ставится знак ⚠️. Описание ошибки можно получить с помощью метода **[получить последнюю ошибку](#getlasterrorполучитьпоследнююошибку)**

## Свойства

### Schema/Схема - Строка⚠️

Хранит главную схему с помощью которой будет происходить валидация JSON с помощью методов компоненты. **При установке значения свойства, происходит компиляция схемы**

Причины исключений:

- Новое значение не является строкой
- Не удалось преобразовать новое значение в JSON
- Не удалось скомпилировать схему

### Format/Формат - Строка⚠️

Содержит формат ошибки проверки JSON с помощью метода [проверить](#validateпроверить). Шаблон по умолчанию **"{path}"**

Причины исключений:

- Новое значение не является строкой

#### Пример:

```bsl
ОбъектКомпоненты.Формат = "Тут вот такая ошибка: {error}";
// Элемент массива ошибок метода "проверить" будет:
// "Тут вот такая ошибка: "123" does not match "^[0-9]{4}$""
```

Доступные шаблоны для подстановки:

- **{error}** - Описывает какое значение и по какой причине не прошло валидацию
- **{path}** - Путь к значению, которое не прошло проверку
- **{instance}** - Значение свойства, которое не прошло проверку
- **{schema_path}** - Путь к ключевому слову JSON Schema, которое не прошло проверку

### UseCustomFormats/ИспользоватьДопФорматы - Булево⚠️

Признак использования дополнительных форматов в ключевом слове **"format"**

Форматы строки:

- **uuid** - формат из спецификации JsonSchema 2019-09. Будет удален после включения в компоненту Draft 2019-09
- **ru-inn-individual** - ИНН физ лица. Производит валидацию ИНН с проверкой контрольной суммы и т.д.
- **ru-inn-legal-entity** - ИНН юр. лица. Производит валидацию ИНН с проверкой контрольной суммы и т.д.

Причины исключений:

- Не удалось преобразовать новое значение в булево

### Version/Версия

Хранит версию компоненты

## Методы

### IsValid/Действителен⚠️

```bsl
// Функция проверяет JSON на соответствие схеме без описания ошибки валидации
//
// Параметры:
//  JsonДляПроверки - Строка - JSON для проверки схемой из свойства Схема
//
// Возвращаемое значение:
//  Булево - Результат проверки
//
ОбъектКомпоненты.Действителен(JsonДляПроверки)
```

Причины исключений:

- Не удалось преобразовать аргумент функции в строку
- Не удалось преобразовать аргумент функции в JSON
- Не установлена [схема](#schemaсхема---строка️)

### AddScheme/ДобавитьСхему⚠️

```bsl
// Процедура добавляет дополнительную схему от которой зависит основная схема из свойства схема
// Сначала необходимо добавить дополнительные схемы, после чего,
// установить основную схему. Иначе основная схема не увидит дополнительные схемы
//
// Параметры:
//  Схема - Строка - дополнительная JSON схема
//
ОбъектКомпоненты.ДобавитьСхему(Схема)
```

Причины исключений:

- Не удалось преобразовать аргумент функции в строку
- Не удалось преобразовать аргумент функции в JSON
- В схеме не найден ключ **$id**
- Значение **$id** не является строкой
- Значение **$id** не удалось преобразовать в [URL](https://url.spec.whatwg.org/)

### DeleteScheme/УдалитьСхему⚠️

```bsl
// Процедура удаляет дополнительную схему по переданному URL
// Если передан не существующий URL, ничего не происходит
//
// Параметры:
//  URL - Строка - URL дополнительной схемы из $id
//
ОбъектКомпоненты.УдалитьСхему(URL)
```

Причины исключений:

- Не удалось преобразовать аргумент функции в строку
- Не удалось преобразовать аргумент функции в [URL](https://url.spec.whatwg.org/)

### DeleteAllSchemes/УдалитьВсеСхемы

```bsl
// Процедура очищает коллекцию дополнительных схем
//
ОбъектКомпоненты.УдалитьВсеСхемы()
```

### GetLastError/ПолучитьПоследнююОшибку

```bsl
// Функция проверяет JSON на соответствие схеме без описания ошибки валидации
// Функцию стоит вызывать сразу после возникновения исключения
// Каждое исключение перезаписывает ошибку
//
// Параметры:
//  JsonДляПроверки - Строка - JSON для проверки схемой из свойства Схема
//
// Возвращаемое значение:
//  Строка - Описание последней возникшей ошибки
//  Неопределено - Если ошибок не происходило
//
ОбъектКомпоненты.Проверить(JsonДляПроверки)
```

### Validate/Проверить⚠️

```bsl
// Функция проверяет JSON на соответствие схеме с описанием ошибок валидации
//
// Параметры:
//  JsonДляПроверки - Строка - JSON для проверки схемой из свойства Схема
//  БуферОшибок - Строка - В этот аргумент будут записаны ошибки валидации JSON в
//						   виде массива строк. Каждый элемент массива будет в
//                         формате из свойства Формат
//
// Возвращаемое значение:
//  Булево - Результат проверки
//
ОбъектКомпоненты.Проверить(JsonДляПроверки, БуферОшибок)
```

Причины исключений:

- Не удалось преобразовать первый аргумент функции в строку
- Не удалось преобразовать первый аргумент функции в JSON
- Не установлена [схема](#schemaсхема---строка️)

## Пример использования

```bsl
Процедура ПроверитьТелоЗапроса()

	КомпонентаПодключена = ПодключитьВнешнююКомпоненту(ПутьККомпоненте(), "JsonСхема",
	ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.НеИзолированно);

	Если Не КомпонентаПодключена Тогда
		Возврат;
	КонецЕсли;

	КомпонентаОбъект = Новый ("AddIn.JsonСхема.JsonSchema1C");

	// Инициализация доп схемы
	Попытка
		КомпонентаОбъект.ДобавитьСхему(ДополнительнаяСхема());
	Исключение
		Сообщить(КомпонентаОбъект.ПолучитьПоследнююОшибку());
	КонецПопытки;

	// Информация о версии
	Сообщить(КомпонентаОбъект.Версия);
	// Отключение доп. форматов
	КомпонентаОбъект.ИспользоватьДопФорматы = Ложь;

	// Установка основной схемы
	Попытка
		КомпонентаОбъект.Схема = ОсновнаяСхема();
	Исключение
		Сообщить(КомпонентаОбъект.ПолучитьПоследнююОшибку());
	КонецПопытки;

	// Установка формата вывода
	КомпонентаОбъект.Формат = "Вот такая вот ошибка {error} вот здесь: {path}";

	// Быстрая проверка
	Попытка
		Сообщить(КомпонентаОбъект.Действителен(JSONДляПроверки()));
	Исключение
		Сообщить(КомпонентаОбъект.ПолучитьПоследнююОшибку());
	КонецПопытки;

	// Подробная проверка
	// Буфер в любом случае будет преобразован в строку
	БуферОшибок = Неопределено;
	Попытка
		КомпонентаОбъект.Проверить(JSONДляПроверки(), БуферОшибок);
	Исключение
		Сообщить(КомпонентаОбъект.ПолучитьПоследнююОшибку());
	КонецПопытки;

	Сообщить(БуферОшибок);

КонецПроцедуры

Функция ОсновнаяСхема()
	Возврат "{
	|  ""type"": ""object"",
	|  ""properties"": {
	|    ""person"": {
	|      ""$ref"": ""https://example.com/person_schema""
	|    }
	|  },
	|  ""required"": [""person""]
	|}";
КонецФункции

Функция ДополнительнаяСхема()
	Возврат "{
	|  ""$id"": ""https://example.com/person_schema"",
	|  ""type"": ""object"",
	|  ""properties"": {
	|    ""name"": {
	|      ""type"": ""string""
	|    },
	|    ""age"": {
	|      ""type"": ""integer"",
	|      ""minimum"": 0
	|    }
	|  },
	|  ""required"": [""name"", ""age""]
	|}
	|";
КонецФункции

Функция JSONДляПроверки()
	Возврат "{ ""person"": { ""name"": 123, ""age"": 30 } }";
КонецФункции

Функция ПутьККомпоненте()
	Возврат "ПутьДоКомпоненты";
КонецФункции
```

Вывод:

```
0.1.0
Нет
["Вот такая вот ошибка 123 is not of type \"string\" вот здесь: /person/name"]
```
